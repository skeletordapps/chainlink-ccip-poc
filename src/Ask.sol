// SPDX-License-Identifier: MIT
pragma solidity 0.8.27;

import {CCIPReceiver} from "@chainlink/contracts-ccip/src/v0.8/ccip/applications/CCIPReceiver.sol";
import {IRouterClient} from "@chainlink/contracts-ccip/src/v0.8/ccip/interfaces/IRouterClient.sol";
import {Client} from "@chainlink/contracts-ccip/src/v0.8/ccip/libraries/Client.sol";
import {IERC20} from
    "@chainlink/contracts-ccip/src/v0.8/vendor/openzeppelin-solidity/v4.8.3/contracts/token/ERC20/IERC20.sol";
import {ICrossChainMessages} from "./interfaces/ICrossChainMessages.sol";
import {console} from "forge-std/Test.sol";

/**
 * @title Ask
 * @notice The contract acts as a sender on the Optimism chain and interacts with the `IRouterClient` to send cross-chain messages.
 * @dev This contract facilitates sending a wallet address to the Base Chain to retrieve tier information using Chainlink CCIP.
 */
contract Ask is CCIPReceiver {
    IRouterClient private router;
    IERC20 private linkToken;
    mapping(address wallet => string name) public tiers;

    /**
     * @dev Constructor initializes the contract with the router address and Link token address.
     * @param _router The address of the `IRouterClient` contract.
     * @param _link The address of the Link token contract.
     */
    constructor(address _router, address _link) CCIPReceiver(_router) {
        router = IRouterClient(_router);
        linkToken = IERC20(_link);
    }

    /**
     * @notice Calculates and returns the estimated fee for sending a cross-chain message to retrieve tier information.
     * @dev This function is a view function and does not modify contract state.
     * @param destinationChainSelector The chain selector of the destination chain.
     * @param receiver The address of the contract on the destination chain that will receive the message.
     * @param wallet The wallet address for which tier information is requested.
     * @return The estimated fee in tokens for sending the message.
     */
    function previewFees(uint64 destinationChainSelector, address receiver, address wallet)
        public
        view
        returns (uint256)
    {
        Client.EVM2AnyMessage memory evm2AnyMessage = mountMessage(receiver, wallet);
        return router.getFee(destinationChainSelector, evm2AnyMessage);
    }

    /**
     * @notice Initiates the process of sending a cross-chain message to retrieve tier information for a wallet.
     * @dev This function performs the following actions:
     *  - Calls `previewFees` to estimate the fee.
     *  - Checks if the contract has sufficient Link tokens for the fee.
     *  - Approves the Link token for the router to spend the required amount.
     *  - Sends the cross-chain message using `ccipSend` on the `IRouterClient`.
     *  - Emits an event `MessageSent` with details about the message.
     * @param destinationChainSelector The chain selector of the destination chain.
     * @param receiver The address of the contract on the destination chain that will receive the message.
     * @param wallet The wallet address for which tier information is requested.
     * @return messageId The message ID generated by the `IRouterClient` for tracking the message.
     */
    function askTier(uint64 destinationChainSelector, address receiver, address wallet)
        external
        returns (bytes32 messageId)
    {
        Client.EVM2AnyMessage memory evm2AnyMessage = mountMessage(receiver, wallet);
        uint256 fees = previewFees(destinationChainSelector, receiver, wallet);

        require(
            fees <= linkToken.balanceOf(address(this)),
            ICrossChainMessages.NotEnoughBalance(linkToken.balanceOf(address(this)), fees)
        );

        linkToken.approve(address(router), fees);
        messageId = router.ccipSend(destinationChainSelector, evm2AnyMessage);
        emit ICrossChainMessages.MessageSent(
            messageId, destinationChainSelector, receiver, wallet, "", address(linkToken), fees
        );

        return messageId;
    }

    /**
     * @notice Callback function that handles incoming cross-chain messages and updates tier information.
     * @dev This function is automatically called by the `CCIPReceiver` contract when a message is received.
     * @param any2EvmMessage The received cross-chain message containing the wallet address and tier name.
     */
    function _ccipReceive(Client.Any2EVMMessage memory any2EvmMessage) internal override {
        (address wallet, string memory tierName) = abi.decode(any2EvmMessage.data, (address, string));

        tiers[wallet] = tierName;
    }

    /**
     * @notice Constructs an `EVM2AnyMessage` structure for sending cross-chain messages.
     * @dev This function encodes the receiver address and wallet address into the message data.
     * @param receiver The address of the contract on the destination chain that will receive the message.
     * @param wallet The wallet address for which tier information is requested.
     * @return evm2AnyMessage The constructed `EVM2AnyMessage` structure.
     */
    function mountMessage(address receiver, address wallet) private view returns (Client.EVM2AnyMessage memory) {
        Client.EVM2AnyMessage memory evm2AnyMessage = Client.EVM2AnyMessage({
            receiver: abi.encode(receiver),
            data: abi.encode(wallet),
            tokenAmounts: new Client.EVMTokenAmount[](0),
            extraArgs: Client._argsToBytes(Client.EVMExtraArgsV1({gasLimit: 500_000})),
            feeToken: address(linkToken)
        });

        return evm2AnyMessage;
    }
}
